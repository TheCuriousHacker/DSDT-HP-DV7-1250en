# ---------------------------------------------------------
# DSDT Battery Fix for HP Pavilion dv7-1250en Notebook PC
# ---------------------------------------------------------
# For using with RehabMan OS-X-ACPI-Battery-Driver, which implements
# an Advanced Configuration and Power Interface (ACPI) based battery
# manager kernel extension (kext/driver) for non-Apple laptops running OS X.
# See: https://github.com/RehabMan/OS-X-ACPI-Battery-Driver
# ---------------------------------------------------------


# ---------------------------------------------------------
# Utility methods for 16bit and 32bit registers
# ---------------------------------------------------------

into method label B1B2 remove_entry;
into definitionblock code_regex . insert
begin
Method (B1B2, 2, NotSerialized) { Return (Or (Arg0, ShiftLeft (Arg1, 8))) }\n
end;

# ---------------------------------------------------------
# Utility methods to read/write buffers from/to EC
# ---------------------------------------------------------

# Remove entries if exists
into method label RE1B parent_label EC remove_entry;
into method label RECB parent_label EC remove_entry;

# Add methods RE1B and RECB
into device label EC insert
begin
Method (RE1B, 1, NotSerialized)\n
{\n
    OperationRegion(ERAM, EmbeddedControl, Arg0, 1)\n
    Field(ERAM, ByteAcc, NoLock, Preserve) { BYTE, 8 }\n
    Return(BYTE)\n
}\n
Method (RECB, 2, Serialized)\n
{\n
    ShiftRight(Arg1, 3, Arg1)\n
    Name(TEMP, Buffer(Arg1) { })\n
    Add(Arg0, Arg1, Arg1)\n
    Store(0, Local0)\n
    While (LLess(Arg0, Arg1))\n
    {\n
        Store(RE1B(Arg0), Index(TEMP, Local0))\n
        Increment(Arg0)\n
        Increment(Local0)\n
    }\n
    Return(TEMP)\n
}\n
end;

# ---------------------------------------------------------
# Utility method to write to EC buffers
# ---------------------------------------------------------

# Remove entries if exists
into method label WE1B parent_label EC remove_entry;
into method label WECB parent_label EC remove_entry;

# Add methods
into device label EC insert
begin
Method (WE1B, 2, NotSerialized)\n
{\n
    OperationRegion(ERAM, EmbeddedControl, Arg0, 1)\n
    Field(ERAM, ByteAcc, NoLock, Preserve) { BYTE, 8 }\n
    Store(Arg1, BYTE)\n
}\n
Method (WECB, 3, Serialized)\n
{\n
    ShiftRight(Arg1, 3, Arg1)\n
    Name(TEMP, Buffer(Arg1) { })\n
    Store(Arg2, TEMP)\n
    Add(Arg0, Arg1, Arg1)\n
    Store(0, Local0)\n
    While (LLess(Arg0, Arg1))\n
    {\n
        WE1B(Arg0, DerefOf(Index(TEMP, Local0)))\n
        Increment(Arg0)\n
        Increment(Local0)\n
    }\n
}\n
end;
